directives:
  autoalias

imports:
  java.util.List
  com.github.forax.smartass.ast.Block
  com.github.forax.smartass.ast.Expr
  com.github.forax.smartass.ast.Lambda
  com.github.forax.smartass.ast.Parameter
  com.github.forax.smartass.ast.Token
  
priorities:
 dot = 8 left
 parens = 7 left
 unary = 6 left
 star = 5 left
 plus = 4 left
 eq = 3 left
 lambda = 2 right
 assign = 1 right

tokens:
  plus= '\+'                   [plus]
  minus= '-'                   [plus]
  star = '\*'                  [star]
  slash = '\/'                 [star]
  mod = '%'                    [star]
  assign= '='                  [assign]
  eq = '=='                    [eq]
  ne = '!='                    [eq]
  lt= '<'                      [eq]
  le = '<='                    [eq]
  gt = '>'                     [eq]
  ge = '>='                    [eq]
  dot = '\.'                   [dot]
  bang = '!' 
  pipe = '\|'      
  at = '@'    
  sharp = '#'
  colon= ':'
  comma=','
  semicolon = ';'
  eol = '\n'
  lpar = '\('                  [parens]
  rpar = '\)' 
  lopt = '\['
  ropt = '\]'
  lcurl = '\{'
  rcurl = '\}'
  _while = 'while'
  _if = 'if'
  _return = 'return'
  _throw = 'throw'
  
  value = "([0-9]+(\.[0-9]+)?)|true|false"
  id = "([a-z]|[A-Z]|_)([a-z]|[A-Z]|[0-9]|_)*"
  text = "'([^'])*'"
  doc = "#([^\r\n])*(\r)?\n"
 
blanks:
 space = "( |\t|\r|\n)"

comments:
 comment = "\/\/([^\r\n])*(\r)?\n"
 
types:
 'id': Token<String>
 'text': Token<String>
 'doc': Token<String>
 'value': Token<String>
 'lopt': int
 'lcurl': int
 'pipe': int
 'colon': int
 
 name: Token<String>
 expr: Expr
 instr: Expr
 instrs: List<Expr>
 block: List<Expr>
 parameter: Parameter
 block_param: Lambda

starts:
  script

productions:
 script = instrs?  { script }
        ;
  
 eoi = 'eol'   { eoi_eol }
     | ';'     { eoi_semi }
     ;
       
 instrs = instr eoi instrs   { instrs_rec }
        | instr eoi          { instrs_instr_eoi }
        | instr              { instrs_instr }
        ;
        
 instr = expr                                       { instr_expr }
       | 'doc'+  expr                               { instr_doc_expr }
       | 'return' expr                              { instr_return }
       | 'throw' expr                               { instr_throw }
       ;
  
 block = instrs?      { block }
       ;
       
 name = 'id'       { name_id }
      | 'text'     { name_text }
      ; 
  
 block_param = ':' block                            { block_param_block }
             | '|' parameter/'comma'* ':' block     { block_param_lambda }
             ;
  
 parameter = name 'id'       { parameter_hint_id }
           | 'id'            { parameter_id }
           ;
 
 expr = 'value'                                               { expr_value }
      | '(' expr ')'                                          { expr_parens }
      | name                                                  { expr_var_access }  
      | 'id' '=' expr                          [assign]       { expr_var_assignment }
      
      | '@' 'id'                                              { expr_field_access }
      | '(' ':' block ')'                                     { expr_block }
      | '(' parameter/'comma'+ ':' block ')'                  { expr_lambda }
      
      | expr '(' expr/'comma'* block_param? ')'               { expr_funcall }
      | expr '.' name '(' expr/'comma'* block_param? ')'      { expr_mthcall }
      
      | 'while' '(' expr ':' block ')'                        { expr_while }
      | 'if' '(' expr ':' block ')'                           { expr_if }
      | 'if' '(' expr ':' block ':' block')'                  { expr_ifelse }
      
      | '!' expr                               [unary]        { expr_unary_not }
      | '-' expr                               [unary]        { expr_unary_neg }
      | '+' expr                               [unary]        { expr_unary_plus }
      | expr '+' expr                          [plus]         { expr_binary_add }
      | expr '-' expr                          [plus]         { expr_binary_sub }
      | expr '*' expr                          [star]         { expr_binary_mul }
      | expr '/' expr                          [star]         { expr_binary_div }
      | expr '%' expr                          [star]         { expr_binary_mod }
      | expr '==' expr                         [eq]           { expr_binary_eq }
      | expr '!=' expr                         [eq]           { expr_binary_ne }
      | expr '<' expr                          [eq]           { expr_binary_lt }
      | expr '<=' expr                         [eq]           { expr_binary_le }
      | expr '>' expr                          [eq]           { expr_binary_gt }
      | expr '>=' expr                         [eq]           { expr_binary_ge }
      ;
   
